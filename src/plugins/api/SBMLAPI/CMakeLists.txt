PROJECT(SBMLAPIPlugin)

MACRO(RETRIEVE_SBML_API_SETTINGS)
    # Retrieve the SBML API settings

    IF(WIN32)
        SET(SBML_API_EXTERNAL_BINARIES
            libsbml${CMAKE_IMPORT_LIBRARY_SUFFIX}
        )
    ELSEIF(APPLE)
        SET(SBML_API_EXTERNAL_BINARIES
            libsbml.5${CMAKE_SHARED_LIBRARY_SUFFIX}
        )
    ELSE()
        SET(SBML_API_EXTERNAL_BINARIES
            libsbml${CMAKE_SHARED_LIBRARY_SUFFIX}.5
        )
    ENDIF()
ENDMACRO()

# Retrieve some header files that were generated on each of our target platforms

SET(CONFIG_FILES
    include/sbml/common/DISTRIB_DIR/libsbml-config-common.h
)

FOREACH(CONFIG_FILE ${CONFIG_FILES})
    STRING(REPLACE "DISTRIB_DIR/" "${DISTRIB_DIR}/" CONFIG_FILE_ORIG "${CONFIG_FILE}")
    STRING(REPLACE "DISTRIB_DIR/" "" CONFIG_FILE_DEST "${CONFIG_FILE}")

    CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/${CONFIG_FILE_ORIG}
                   ${PROJECT_SOURCE_DIR}/${CONFIG_FILE_DEST}
                   COPYONLY)
ENDFOREACH()

# Retrieve the plugin's binary file(s)

STRING(REPLACE "${${CMAKE_PROJECT_NAME}_SOURCE_DIR}/" "" RELATIVE_PROJECT_SOURCE_DIR "${PROJECT_SOURCE_DIR}/bin/${DISTRIB_BINARY_DIR}")

IF(WIN32)
    IF(${ARCHITECTURE} EQUAL 32)
        IF(RELEASE_MODE)
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.dll 888ac2a300796235f169da857fb31670e707420b)
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.lib 800eb94202647c8c1245efcf698c76bec5db41a7)
        ELSE()
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.dll 02fd4e51d562fb4bc02db024dbedf6e5806645b8)
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.lib aa93ee2add4cad1e53fc3f66ccb773bd3ab0598e)
        ENDIF()
    ELSE()
        IF(RELEASE_MODE)
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.dll e2fb73caa0cb20d6722a66638ce2a26233dc875f)
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.lib eab202a68ddcb5ab0391ed149a5423d9222ffd42)
        ELSE()
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.dll db2df210af341e833fb79021ddda4ce1b8b31d0b)
            RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.lib 35d17fda6341331e1fb532f7b0742fd4ea94fe72)
        ENDIF()
    ENDIF()
ELSEIF(APPLE)
    RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.5.dylib 15442f77f9184d02c4ffe602dfd31ff98b0adc90)
ELSE()
    IF(${ARCHITECTURE} EQUAL 32)
        RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.so.5 c411b1c703b5904d164b34f2aba548cd9fa650a4)
    ELSE()
        RETRIEVE_BINARY_FILE(${RELATIVE_PROJECT_SOURCE_DIR} libsbml.so.5 ea03ccea4a4749d5e54f503a96696fd845c3d283)
    ENDIF()
ENDIF()

# SBML API libraries

SET(SBML_API_EXTERNAL_BINARIES_DIR ${PROJECT_SOURCE_DIR}/bin/${DISTRIB_BINARY_DIR})

RETRIEVE_SBML_API_SETTINGS()

# Add the plugin

ADD_PLUGIN(SBMLAPI
    SOURCES
        ../../plugininfo.cpp

        src/sbmlapiplugin.cpp
    HEADERS_MOC
        src/sbmlapiplugin.h
    INCLUDE_DIRS
        include
    QT_MODULES
        Core
    QT_LIBRARIES
        QtCore
    EXTERNAL_BINARIES_DIR
        ${SBML_API_EXTERNAL_BINARIES_DIR}
    EXTERNAL_BINARIES
        ${SBML_API_EXTERNAL_BINARIES}
    TESTS
        tests
)

# Deploy the plugin's external binaries
# Note: it must be done here since ADD_PLUGIN doesn't support the fact that an
#       external binary may have dependencies (something that is important on OS
#       X)...

IF(WIN32)
    FOREACH(SBML_API_EXTERNAL_BINARY_FOR_DEPLOYMENT ${SBML_API_EXTERNAL_BINARIES_FOR_DEPLOYMENT})
        WINDOWS_DEPLOY_LIBRARY(${SBML_API_EXTERNAL_BINARIES_DIR}
                               ${SBML_API_EXTERNAL_BINARY_FOR_DEPLOYMENT}${CMAKE_SHARED_LIBRARY_SUFFIX})
    ENDFOREACH()
ELSEIF(APPLE)
    OS_X_DEPLOY_LIBRARY(${SBML_API_EXTERNAL_BINARIES_DIR} sbml.5)
ELSE()
    FOREACH(SBML_API_EXTERNAL_BINARY ${SBML_API_EXTERNAL_BINARIES})
        LINUX_DEPLOY_LIBRARY(${SBML_API_EXTERNAL_BINARIES_DIR}
                             ${SBML_API_EXTERNAL_BINARY})
    ENDFOREACH()
ENDIF()
